AC_INIT(FileZilla, 2.9, tim.kosse@gmx.de)
AC_CONFIG_AUX_DIR(config)
AM_INIT_AUTOMAKE([dist-bzip2 dist-zip])

AC_CANONICAL_HOST

AC_PROG_CXX
AC_PROG_INSTALL
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL

AC_CACHE_SAVE

CFLAGS="$CFLAGS -Wall -g -fexceptions"
CXXFLAGS="$CXXFLAGS -Wall -g -fexceptions"

AM_OPTIONS_WXCONFIG

AM_PATH_WXCONFIG(2.5.3, wxWin=1, , [xrc,std])
if test "$wxWin" != 1; then
	AC_MSG_ERROR([
        	wxWidgets must be installed on your system
		but wx-config script couldn't be found.

		Please check that wx-config is in path, the directory
		where wxWidgets libraries are installed (returned by
		'wx-config --libs' command) is in LD_LIBRARY_PATH or
		equivalent variable and wxWidgets version is 2.5.3 or above.
	])
fi

AC_PATH_PROG([WXRC], [wxrc --version])
if test "x$WXRC" = "x"; then
	AC_MSG_ERROR([
		wxrc could not be found. This program gets build together with wxWidgets.
		Please make sure wxrc is within your path.
	])
fi

AC_CACHE_SAVE

CPPFLAGS="$CPPFLAGS $WX_CPPFLAGS"
CXXFLAGS="$CXXFLAGS $WX_CXXFLAGS_ONLY"
CFLAGS="$CFLAGS $WX_CFLAGS_ONLY"
LDFLAGS="$LDFLAGS $WX_LIBS"

WINDRESFLAGS=
use_resourcefile=
if test "x$host_os" = "xcygwin" -o "x$host_os" = "xmingw32"; then
        if wx-config --cppflags | grep __WXMSW__; then
		if windres --version; then
                  use_resourcefile=true
                fi
	fi
	# I hate Quadrigraphs
	# fixme: hardcoded -mthreads removal is no solution, I need a real 
        # filter for defines and include paths
	WINDRESFLAGS=`echo $CPPFLAGS | sed 's/\(^\|@<:@ @:>@\)-mthreads\($\|@<:@ @:>@\)/ /g'`
fi

AM_CONDITIONAL(USE_RESOURCEFILE, test x$use_resourcefile = xtrue)
AM_CONDITIONAL(MACAPPBUNDLE, wx-config --cppflags | grep __WXMAC__)

AC_CHECK_HEADER(idna.h,, 
[
	AC_MSG_ERROR([idna.h not found which is part of GNU libidn.])
])

AC_ARG_WITH(idn-lib,
[[  --with-idn-lib=FILE     Use the given path to the idn library.]],
[
	if test "$withval" != "yes" -a "$withval" != ""; then
		IDN_LIB=$withval
	fi
])

if test "x$IDN_LIB" = "x"; then
	AC_CHECK_LIB(idn, idna_to_ascii_8z, IDN_LIB="-lidn",
		AC_MSG_ERROR([GNU libidn not found. Try using --with-idn-lib=FILE to specify the library path.])
	)
fi

AC_MSG_CHECKING([available FZ3 message catalogs])
FILEZILLA_LINGUAS_PO=`ls locales/*.po | grep '\.po' | sed -e 's/\(.\+\/\)\?\(@<:@a-z@:>@*\(_@<:@A-Z@:>@*\)\?\)\.po/\2.po/g' | tr '\n' ' ' | sed -e 's/ \+/ /g'`
FILEZILLA_LINGUAS=`echo $FILEZILLA_LINGUAS_PO | tr -d '.po'`
AC_MSG_RESULT([$FILEZILLA_LINGUAS])

CATALOGS_TO_INSTALL="install-filezilla-catalogs"

LIBS="$LIBS $IDN_LIB"

AC_SUBST(WX_LIBS)
AC_SUBST(CATALOGS_TO_INSTALL)
AC_SUBST(FILEZILLA_LINGUAS)
AC_SUBST(FILEZILLA_LINGUAS_PO)
AC_SUBST(WINDRESFLAGS)

AC_OUTPUT(Makefile src/Makefile src/engine/Makefile src/tinyxml/Makefile
src/interface/Makefile src/interface/resources/Makefile src/include/Makefile
locales/Makefile src/interface/resources/16x16/Makefile
src/interface/resources/32x32/Makefile data/Makefile)
